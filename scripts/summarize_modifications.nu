#!/usr/bin/env nu
# Script to summarize the modifications per gene and bin into 4 categories
def main [
        mods: string, # Path to the per read modifications file generated by Modkit Extract Full
        mapped:string, # Path to the mapped file generated by bedtools intersect -a hg38.refGene.bed.gz -b aln.bam -wa -wb > mapped_features.txt
        output: string  # Path to the output csv
        ] {
    let df_mods = polars open -t tsv -d "\t" $mods 
    let df_mapped = polars open --no-header -t tsv -d "\t" $mapped | 
        polars select column_1 column_4 column_6 column_14 column_15 column_16 | 
        polars rename [column_1 column_4 column_6 column_14 column_15 column_16] ["Chromosome" "Gene" "Strand" "Gene Start" "Gene Stop" "Read ID"]

    let df_combined = $df_mods | 
        polars join $df_mapped [read_id chrom] ["Read ID" "Chromosome"] --left
    
    let df_summary = $df_combined | 
        polars with-column (
        polars when (
            (polars col mod_qual ) > 0.75) "High >=0.75" | 
                polars when ((polars col mod_qual ) >= 0.50) "Med >=0.50" | 
                polars when ((polars col mod_qual ) >= 0.25) "Low >=0.25" | 
                polars otherwise "V Low / Unmodified <0.25" | 
                polars as "Modifications Probability"
        )| 
        polars group-by Gene chrom "Modifications Probability" | 
                polars agg [
                (polars col canonical_base | polars count | polars as "Count")
            ] |
        polars filter ((polars col Gene) != "") |
        polars sort-by chrom Gene 
        
    let df_mod_summary = $df_summary | 
        polars pivot --streamable --on ["Modifications Probability"] --index [Gene chrom] --values [Count]

    $df_combined |
        polars group-by Gene chrom | 
            polars agg [
            (polars col canonical_base | polars count | polars as "Total A")
            (polars col "Gene Start" | polars first | polars as "Gene Start")
            (polars col "Gene Stop" | polars first | polars as "Gene Stop")
            (polars col read_length | polars min | polars as "Min Read Length")
            (polars col read_length | polars max | polars as "Max Read Length")
            (polars col read_length | polars mean | polars as "Mean Read Length")
        ] |  polars join $df_mod_summary [Gene chrom] [Gene chrom] --left |
        polars sort-by chrom Gene |
        polars collect  |
        polars save $output
}